########################################################################
# Conversion of harmonie grib1 surface parameters into 
#   uerra grib2 compliant ones
########################################################################
#
# - it contains rules for grib_filter tool
# - variables @type@ (=an/fc), @version@ (=8/9) and @outdir@ 
#   (output directory) must be parsed before running it
#
# How to run it:
# - parse variables above to get the final script
# - grib_filter <the final script name> <input grib1 files>
#
# Output:
# - grib2 files in @outdir@
########################################################################

transient parType = "";
transient param2 = -999;
transient sl1 = -999;


transient levtype_str = "";

transient debug = 0;

switch (param) {

   case 193.1:                         # Surface soil ice (2 levels)   
     set param2 = 260644;
     set typeOfLevel = 'depthBelowLand';  
     set parType = 'soil_level';

   default: set param2 = -999;

} 




if (debug==1){
  print "1:debug parType=[parType], levtype=[levtype],  paramId=[paramId], param=[param], param2=[param2]";
}

if (param2 != -999) {
  print "param2=[param2]";


  ################# major start ###################
  if (LoV < 0) {
    set LoV = 360000 + LoV; # GRIB1 angles are in millidegrees
  }
  
  set centre = "ecmf"; # generaly used hack for conversion..
  set paramId = 130; # another generaly usable hack.. (neccessary when grib2 def does not exist at all)

  set edition = 2;
  
 
  set paramId = param2;
  
  # some special set up for some special groups of params..
  switch (parType) {
   case "soil_level":
     set paramId = param2;
     set typeOfFirstFixedSurface = 151;
     set scaleFactorOfFirstFixedSurface = 0;
     set scaledValueOfFirstFixedSurface = level;
     set typeOfSecondFixedSurface = 255;
     set scaleFactorOfSecondFixedSurface = MISSING;
     set scaledValueOfSecondFixedSurface = MISSING;
   case "soil_layer":
     set typeOfFirstFixedSurface = 151;
     set scaleFactorOfFirstFixedSurface = 0;
     set scaledValueOfFirstFixedSurface = level_prev;
     set typeOfSecondFixedSurface = 151;
     set scaleFactorOfSecondFixedSurface = 0;
     set scaledValueOfSecondFixedSurface = level;
   case "cc":
     # lcc/mcc/hcc
     set typeOfFirstFixedSurface = 1; # by default it is 103!
     set scaleFactorOfFirstFixedSurface = MISSING;
     set scaledValueOfFirstFixedSurface = MISSING;
     set typeOfSecondFixedSurface = 8;
     set scaleFactorOfSecondFixedSurface = MISSING;
     set scaledValueOfSecondFixedSurface = MISSING;
   case "accum":
     set scaleFactorOfFirstFixedSurface = MISSING;
     set scaledValueOfFirstFixedSurface = MISSING;
     set typeOfStatisticalProcessing = 1;
   case "surface":
     set typeOfFirstFixedSurface = 1;
   default:
     #print "No special set up for parType=[parType]";
  }
  # some special set up needed for some params..
  if (param2==260509 || param2==33 || param2==173 || param2==3066 || param2==174008 || param2==260015 || param2==260651){
    set typeOfFirstFixedSurface = 1; # by default it is 103!
  }
#  # ES TEST
#  if (param2==174098){
#    set typeOfFirstFixedSurface = 1; # Should be 160 but fails in mars
#  }

  # Snow on ice total depth, sea ice surface temperature
  if (param2==260650 || param2==260649){
    set typeOfFirstFixedSurface = 174;
    set typeOfSecondFixedSurface = 255;
  }

  # Local number for some params
  if (param2==260651){
    set localTablesVersion = 1;
  }

  # Cloud base and cloud top
  if (param2==260107 || param2==260108){
    set typeOfFirstFixedSurface = 1;
    set typeOfSecondFixedSurface = 255;
  }

  if (typeOfFirstFixedSurface==1 || typeOfFirstFixedSurface==177 || typeOfFirstFixedSurface==101){
    set scaleFactorOfFirstFixedSurface = MISSING;
    set scaledValueOfFirstFixedSurface = MISSING;
  }
  # set up finish
  set centre = "enmi";
  set productionStatusOfProcessedData = 10; #@version@;
  set tablesVersion = 23;
  set grib2LocalSectionPresent = 1;
  set type = "@type@";
#  set origin = "enmi-haro-cae";
#  set class = "RR";

  if (parType is "accum") {
    set stepType = "accum";
  }

  if (parType is "soil_layer") {
  # this is intentional hack!
  #   this set up must be repeated here otherwise scaledValueOfFirstFixedSurface will equal to level!!
    set typeOfFirstFixedSurface = 151;
    set scaleFactorOfFirstFixedSurface = 0;
    set scaledValueOfFirstFixedSurface = level_prev;
  }

  ################# major end #####################
  # some checks..
  assert(stream is "oper");
  assert(type is "@type@");
#  assert(is_uerra==1);
#  assert(is_precise==1);
  assert(productionStatusOfProcessedData==10); #@version@);

  set levtype_str = levtype;
  if (levtype == 177){
    # JIRA issue GRIB-871
    set levtype_str = "sfc";
  }
  if (levtype == 151){
    # JIRA issue GRIB-871
    set levtype_str = "sol";
  }
  if (debug == 1){
    print "[param]";
  }
  print "[param] [levtype] [levtype_str] [level]";
  append"[type].[date].[time].sol.grib2";

}
