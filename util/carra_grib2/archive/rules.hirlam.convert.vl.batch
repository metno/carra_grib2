########################################################################
# Conversion of harmonie grib1 vertical level parameters(ml/pl/hl) into 
#   uerra grib2 compliant ones
########################################################################
#
# - it contains rules for grib_filter tool
# - variables @type@ (=an/fc), @version@ (=8/9) and @outdir@ 
#   (output directory) must be parsed before running it
#
# How to run it:
# - parse variables above to get the final script
# - grib_filter <the final script name> <input grib1 files>
#
# Output:
# - grib2 files in @outdir@
########################################################################

transient debug = 0;
transient param2 = -999;


switch (param) {
      
  case 71.253: set param2 = 260257; # cloud cover
  case  6.253: set param2 = 129;    # geopotential
  case  1.253: set param2 = 54;     # pressure
  case 76.253: set param2 = 246;    # specific cloud liquid water content
  case 58.253: set param2 = 247;    # specific cloud ice water content
  case 51.253: set param2 = 133;    # specific humidity
  case 52.253: set param2 = 157;    # relative humidity
  case 11.253: set param2 = 130;    # temperature
  case 31.253: set param2 = 3031;   # wind direction
  case 32.253: set param2 = 10;     # wind speed
  case 33.253: set param2 = 131;    # u-velocity
  case 34.253: set param2 = 132;    # v-velocity
  case 200.253: set param2 = 260155;   # TKE
  case 40.253: set param2 = 260238; # vertical velocity
  case 4.253: set param2 = 60;      # Potential vorticity
  case 14.253: set param2 = 3014; # Pseudo-adiabatic potential temperature
  case 181.253: set param2 = 75;       # specific rain water content
  case 184.253: set param2 = 76;      # --||-- snow --||--
  case 201.253: set param2 = 260028;  # --||-- grauple --||--

  
  default:     set param2 = -999;
}

if (debug == 1){
  print "1:debug indicatorOfTypeOfLevel=[indicatorOfTypeOfLevel:l] level=[level] levtype=[levtype] paramId=[paramId] param=[param] param2=[param2]";
}


if (param2 != -999) {

    if (LoV < 0) {
        set LoV = 360000 + LoV; # GRIB1 angles are in millidegrees
    }
    set centre = "ecmf"; # generaly used hack for conversion..
    set paramId = 130; # another generaly usable hack.. (neccessary when grib2 def does not exist at all)
    set edition=2;
    set paramId = param2;
    set centre="enmi";
    set productionStatusOfProcessedData=10;
    set tablesVersion=23;
    set grib2LocalSectionPresent=1;
    set type = "@type@";
    # some checks..
    assert(stream is "oper");
    assert(type is "@type@");
#    assert(is_uerra == 1);
#    assert(is_precise == 1);
    assert(productionStatusOfProcessedData==10);

    write "@outdir@/[type].[date].[time].[levtype].grib2";
}

